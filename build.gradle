buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.hibernate.build:gradle-maven-publish-auth:3.0.4"
    }
}

plugins {
    id 'net.researchgate.release' version '3.0.2'
}

subprojects {    

    group = "org.recast4j"

    repositories {
        mavenLocal()
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    if (!project.hasProperty('ci')) {
        apply plugin: 'org.hibernate.build.maven-repo-auth'
    }

    dependencies {
        testImplementation (libs.junit5.api)
        testImplementation (libs.junit5.params)
        testImplementation (libs.assertj.core)
        testImplementation (libs.mockito)
        testRuntimeOnly (libs.junit5.engine)
    }

    java {
        if (!project.hasProperty('ci')) {
            withSourcesJar()
            withJavadocJar()
        }
    }

    compileJava {
        options.encoding = 'UTF-8'
        if (JavaVersion.current().isJava9Compatible()) {
            options.release = 8
        }
    }
    
    javadoc {
        options.encoding = 'UTF-8'    
        options.addBooleanOption('Xdoclint:none', true)
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }
    
    test {
        useJUnitPlatform()
    }

    jar {
        manifest { attributes('Implementation-Version': archiveVersion) }
    }
    
    if (it.name != 'recast-demo') {

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from components.java
                    pom {
                        name = 'Recast and Detour for Java'
                        description = 'Navigation Mesh Construction and Path-finding Toolkit.'
                        url = 'http://recast4j.org'
                        licenses {
                            license {
                                name = 'Zlib License'
                                url = 'https://github.com/ppiastucki/recast4j/blob/master/License.txt'
                            }
                        }
                        developers {
                            developer {
                                name = 'Piotr Piastucki'
                                email = 'piotr@recast4j.org'
                            }
                        }
                        scm {
                            connection = 'scm:git:https://github.com/ppiastucki/recast4j.git'
                            developerConnection = 'scm:git:ssh://git@github.com:ppiastucki/recast4j.git'
                            url = 'https://github.com/ppiastucki/recast4j'
                        }
                    }                    
                }
            }
            
            repositories {
                maven {
                    name = 'ossrh'
                    def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                    def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
                    url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                }
            }
        }

        signing {
            sign publishing.publications.mavenJava
        }
    }

}

// https://github.com/researchgate/gradle-release/issues/144
//task build() {
//    dependsOn subprojects.build
//}

task publish() {
    dependsOn subprojects.publish
}

afterReleaseBuild.dependsOn publish
